"
" Specify a directory for plugins (for Neovim: ~/.local/share/nvim/plugged)
call plug#begin('~/.vim/plugged')

" Make pairs colorful
Plug 'luochen1990/rainbow'
let g:rainbow_active = 1 "0 if you want to enable it later via :RainbowToggle"

if !has("gui_running") 
    Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
    Plug 'junegunn/fzf.vim'
    let g:fzf_layout = { 'down': '~30%'  }
    map <silent> <leader>p :GFiles<cr>
    map <silent> <leader>bo :Buffers<cr>
    map <silent> <leader>gs :GFiles?<cr>
    map <silent> <leader>gl :Commits<cr>
    map <silent> <leader>gc :BCommits<cr>
    map <silent> <leader>t :Tags<cr>

    command! -bang -nargs=* Ag
                \ call fzf#vim#ag(<q-args>,
                \                 <bang>0 ? fzf#vim#with_preview('up:60%')
                \                         : fzf#vim#with_preview('right:50%:hidden', '?'),
                \                 <bang>0)
    command! -bang -nargs=? -complete=dir GFiles
                \ call fzf#vim#gitfiles(<q-args>, fzf#vim#with_preview(), <bang>0)
    command! -bang -nargs=? -complete=dir Files
                \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)
endif

" Search file
Plug 'ctrlpvim/ctrlp.vim'
if executable('ag')
    let g:ctrlp_user_command = 'ag %s -l --nocolor --hidden -g ""'
endif
let g:user_command_async = 1
let g:ctrlp_lazy_update = 1
let g:ctrlp_working_path_mode = 'r'   
let g:ctrlp_by_filename = 1

" Search
Plug 'mileszs/ack.vim' 
let g:ack_use_cword_for_empty_search = 1
if executable('ag')
    let g:ackprg = 'ag --vimgrep'
endif
vnoremap <silent> <leader>f :call visual#action('Ack! foo')<cr>
nnoremap <leader>f :Ack!<space>

" Replace ctags.
" Use :CodeQueryMakeDB + 'language name' to generate db file.
" Use :CodeQuery for undercurser tag search.
Plug 'PangPangPangPangPang/vim-codequery'
map <silent> <leader>c :CodeQuery<CR>

if has("cscope")
  set csto=1
  set cst
  set nocsverb
  " add any database in current directory
  let find_str = system("find ./ -name '*.out' -maxdepth 1")
  let file_list = split(find_str, '\n')
  if len(file_list) > 0
      let file_name = file_list[0]
      if filereadable(file_name)
          let r = 'cs add ' . file_name
          execute r
      endif
  endif
  set csverb
endif
map <leader>cg :cs find g <C-R>=expand("<cword>")<CR><CR>

"""""""""""""""""""statusline""""""""""""""""""""
Plug 'itchyny/lightline.vim'
let g:lightline = {
            \ 'colorscheme': 'PaperColor',
            \ 'mode_map': { 'c': 'NORMAL'  },
            \ 'active': {
            \   'left': [ [ 'mode', 'paste'  ], [ 'fugitive', 'filename'  ]  ],
            \   'right': [ [ 'ale' ], [ 'lineinfo'  ], ['percent'], [ 'fileformat', 'fileencoding', 'filetype'  ]  ]
            \ },
            \ 'inactive': {
            \   'left': [ [ 'filename' ]  ],
            \   'right': []
            \ },
            \ 'component_function': {
            \   'modified': 'LightlineModified',
            \   'readonly': 'LightlineReadonly',
            \   'fugitive': 'LightlineFugitive',
            \   'filename': 'LightlineFilename',
            \   'fileformat': 'LightlineFileformat',
            \   'filetype': 'LightlineFiletype',
            \   'fileencoding': 'LightlineFileencoding',
            \   'mode': 'LightlineMode',
            \   'ale': 'LightlineAle',
            \ },
            \ 'separator': { 'left': '', 'right': '' },
            \ 'subseparator': { 'left': '', 'right': '' }
            \ }

function! LightlineAle()
    if exists('g:loaded_ale')
        return ALEGetStatusLine()
    endif
    return ''
endfunction

function! LightlineModified()
    return &ft =~ 'help\|vimfiler\|gundo' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! LightlineReadonly()
    return &ft !~? 'help\|vimfiler\|gundo' && &readonly ? '' : ''
endfunction

function! LightlineMode()
    let fname = expand('%:t')
    return fname == '__Tagbar__' ? 'Tagbar' :
                \ fname == 'ControlP' ? 'CtrlP' :
                \ fname =~ 'NERD_tree' ? 'NERDTree' :
                \ winwidth(0) > 60 ? lightline#mode() : ''
endfunction

function! LightlineFilename()
    let fname = expand('%:t')
    if fname == 'ControlP'
        return ''
    endif
    if fname =~ 'NERD_tree'
        return 'NERDTree'
    endif
    if fname =~ 'FZF'
        return 'FZF'
    endif
    if fname =~ '__Tagbar__'
        return 'Tagbar'
    endif

    return ('' != LightlineReadonly() ? LightlineReadonly() . ' ' : '') .
                \ (&ft == 'vimfiler' ? vimfiler#get_status_string() :
                \  &ft == 'unite' ? unite#get_status_string() :
                \  &ft == 'vimshell' ? vimshell#get_status_string() :
                \ '' != expand('%:t') ? expand('%:t') : '[No Name]') .
                \ ('' != LightlineModified() ? ' ' . LightlineModified() : '')
endfunction

function! LightlineFugitive()
    if &ft !~? 'vimfiler\|gundo' && exists("*fugitive#head")
        let branch = fugitive#head()
        return branch !=# '' ? ' '.branch : ''
    endif
    return ''
endfunction

function! LightlineFileformat()
    return winwidth(0) > 70 ? &fileformat : ''
endfunction

function! LightlineFiletype()
    return winwidth(0) > 70 ? (&filetype !=# '' ? &filetype : 'no ft') : ''
endfunction

function! LightlineFileencoding()
    return winwidth(0) > 70 ? (&fenc !=# '' ? &fenc : &enc) : ''
endfunction
"""""""""""""""""""""""""""""""""""""""

Plug 'scrooloose/nerdtree', {'on': ['NERDTreeTabsToggle', 'NERDTreeFind']}
" Ignore files in NERDTree
let NERDTreeIgnore=['\.pyc$', '\~$'] 
Plug 'Xuyuanp/nerdtree-git-plugin', {'on': ['NERDTreeTabsToggle', 'NERDTreeFind']}
Plug 'jistr/vim-nerdtree-tabs', {'on': ['NERDTreeTabsToggle', 'NERDTreeFind']}
let g:nerdtree_tabs_open_on_gui_startup = 0
map <silent> <F1> :NERDTreeTabsToggle<cr>
imap <silent> <F1> <Esc>:NERDTreeTabsToggle<cr>
map <silent> <leader>j :NERDTreeFind<cr>

" Config netrw
let g:netrw_banner = 0
let g:netrw_liststyle = 3 " tree view
let g:netrw_altv = 1 " open splits to the right
let g:netrw_list_hide = netrw_gitignore#Hide()
let g:netrw_list_hide = ',\(^\|\s\s\)\zs\.\S\+'

Plug 'majutsushi/tagbar', {'on': 'TagbarToggle'}
map <silent> <F2> :TagbarToggle<cr>
imap <silent> <F2> <Esc>:TagbarToggle<cr>

" Use :GutentagsUpdate to update tags
" Plug 'ludovicchabant/vim-gutentags'

" Efficiency.
Plug 'easymotion/vim-easymotion'

Plug 'terryma/vim-multiple-cursors'
let g:multi_cursor_quit_key='<C-c>'
let g:multi_cursor_skip_key='<C-x>'

Plug 'jiangmiao/auto-pairs'
Plug 'tpope/vim-surround'

" Add commenter
Plug 'scrooloose/nerdcommenter'
let g:NERDSpaceDelims=1
map <leader>/ <leader>c<space>

" Code snip support
let s:snipmate_installed = 0
if has("mac")
    " UltiSnips slow down neovim performance, change to vim-snipmate
    Plug 'MarcWeber/vim-addon-mw-utils' | Plug 'tomtom/tlib_vim' | Plug 'garbas/vim-snipmate', {'on': []}
    let s:snipmate_installed = 1
else
    Plug 'SirVer/ultisnips', {'on': []}
    let g:UltiSnipsExpandTrigger="<tab>"
    let g:UltiSnipsJumpForwardTrigger="<c-n>"
    let g:UltiSnipsJumpBackwardTrigger="<c-p>"
endif
Plug 'PangPangPangPangPang/vim-snippets', {'on': []}
""""""""""""""""""""""""""""""""""""""""""

"""""""""""""""""""git""""""""""""""""""""
Plug 'tpope/vim-fugitive' 
map <leader>ga :Gwrite<cr>
map <silent> <leader>gb :Gblame<cr>
""""""""""""""""""""""""""""""""""""""""""

"""""""""""""""""""Syntastic""""""""""""""
if v:version > 743 || has('nvim')
    Plug 'w0rp/ale'
    " Set ale disable as default
    let g:ale_enabled = 0
    let g:ale_lint_on_text_changed = 'normal'
    " let g:ale_lint_on_enter = 0

    let g:ale_sign_error = '✖'
    let g:ale_sign_warning = '◎'
    let g:ale_statusline_format = ['⨉ %d', '⚠ %d', '⬥ ok']
    highlight clear ALEErrorSign
    highlight clear ALEWarningSign
    " let g:ale_sign_column_always = 1
    "
    let g:ale_linters = {'jsx': ['stylelint', 'eslint'],
                \'python' : ['flake8']
                \ }
    let g:ale_linter_aliases = {'jsx': 'css'}
    map <silent> <leader>s :ALEToggle<cr>
else
    Plug 'vim-syntastic/syntastic'
    set statusline+=%#warningmsg#
    set statusline+=%{SyntasticStatuslineFlag()}
    set statusline+=%*

    let g:syntastic_always_populate_loc_list = 1
    let g:syntastic_auto_loc_list = 1
    let g:syntastic_check_on_open = 1
    let g:syntastic_check_on_wq = 0

    " use eslint to check javascript syntastic
    " npm install eslint-plugin-import eslint-plugin-jsx-a11y eslint-plugin-react eslint babel-eslint eslint_d -g
    let g:syntastic_javascript_checkers = ['eslint'] 
    let g:syntastic_javascript_eslint_exec = 'eslint_d' 

    " use pylint to check python syntastic
    " pip install pylint
    " if you use zsh and osx add `export LC_CTYPE=en_US.UTF-8` in your .zshrc
    let g:syntastic_python_checkers = ['pylint'] 
    let g:syntastic_python_python_exec = 'python2'
endif
"""""""""""""""""""""""""""""""""""""""""""

"""""""""""Complete & Syntastic"""""""""""""""""
Plug 'Valloric/YouCompleteMe', {'on': []}
if v:version > 743 || has('nvim')
    let g:ycm_register_as_syntastic_checker = 0 "default 1
    let g:ycm_enable_diagnostic_signs = 0
    let g:ycm_enable_diagnostic_highlighting = 0
else
    let g:ycm_register_as_syntastic_checker = 1 "default 1
    let g:ycm_enable_diagnostic_signs = 1
    let g:ycm_enable_diagnostic_highlighting = 1
    let g:ycm_error_symbol = '✖'
    let g:ycm_warning_symbol = '◎'
endif


let g:Show_diagnostics_ui = 1 "default 1
""will put icons in Vim's gutter on lines that have a diagnostic set.
"Turning this off will also turn off the YcmErrorLine and YcmWarningLine
""highlighting
let g:ycm_always_populate_location_list = 1 "default 0
let g:ycm_open_loclist_on_ycm_diags = 1 "default 1
let g:ycm_key_list_select_completion = ['<Enter>', '<Down>']

let g:ycm_complete_in_strings = 1 "default 1
let g:ycm_collect_identifiers_from_tags_files = 0 "default 0
let g:ycm_path_to_python_interpreter = '' "default ''

" support css file auto complete
let g:ycm_semantic_triggers = {
            \   'css': [ 're!^\s{2}', 're!:\s+' ],
            \   'html': [ '</' ],
            \ }

let g:ycm_server_use_vim_stdout = 0 "default 0 (logging to console)
let g:ycm_server_log_level = 'info' "default info

let g:ycm_global_ycm_extra_conf = '~/.ycm_extra_conf.py'  "where to search for .ycm_extra_conf.py if not found
let g:ycm_confirm_extra_conf = 1

let g:ycm_goto_buffer_command = 'same-buffer' "[ 'same-buffer', 'horizontal-split', 'vertical-split', 'new-tab'  ]
let g:ycm_filetype_whitelist = { '*': 1  }
let g:ycm_key_invoke_completion = '<C-Space>'

" let g:ycm_add_preview_to_completeopt = 1
" let g:ycm_autoclose_preview_window_after_insertion = 1
" set completeopt-=preview
" set splitbelow

" nnoremap <leader>gl :YcmCompleter GoToDeclaration<CR>
" nnoremap <leader>gf :YcmCompleter GoToDefinition<CR>
nnoremap <silent> <c-]> a<ESC>:YcmCompleter GoToDefinitionElseDeclaration<CR>
nmap <F4> :YcmDiags<CR>
nnoremap <F11> :YcmForceCompileAndDiagnostics <CR>
"""""""""""""""""""""""""""""""""""""""""""
""""""""""""""Lazyload func""""""""""""""""
augroup plug_lazy_load
    if s:snipmate_installed == 1
        autocmd!
        autocmd InsertEnter * call plug#load('vim-addon-mw-utils', 'tlib_vim', 'vim-snipmate', 'vim-snippets', 'YouCompleteMe')
                    \| autocmd! plug_lazy_load
    else
        autocmd!
        autocmd InsertEnter * call plug#load('YouCompleteMe', 'ultisnips','vim-snippets')
                    \| autocmd! plug_lazy_load
    endif
augroup END
"""""""""""""""""""""""""""""""""""""""""""

"""""""""""""""""Python""""""""""""""""""""
let g:python_highlight_all = 1
Plug 'vim-python/python-syntax'
"""""""""""""""""""""""""""""""""""""""""""

"""""""""""""""""""""FE""""""""""""""""""""
Plug 'pangloss/vim-javascript'
Plug 'othree/javascript-libraries-syntax.vim'
let g:used_javascript_libs = 'requirejs,vue,react,jquery'
Plug 'othree/xml.vim', {'for': ['html', 'xml']}

"react support
Plug 'maxmellon/vim-jsx-pretty'
"""""""""""""""""""""""""""""""""""""""""""

"""""""""""""""""""Swift"""""""""""""""""""
Plug 'keith/swift.vim', {'for': 'swift'}
"Swift highlight
set runtimepath+=~/.vim/plugged/swift.vim
"""""""""""""""""""""""""""""""""""""""""""

""""""""""""""markdown"""""""""""""""""
" npm -g install instant-markdown-d
Plug 'suan/vim-instant-markdown', {'for': 'markdown'}
""""""""""""""""""""""""""""""""""""""""""
" Initialize plugin system

Plug 'PangPangPangPangPang/visual-selection-vim'
" Visual mode pressing * or # searches for the current selection
vnoremap <silent> * :<C-u>call visual#action('')<CR>/<C-R>=@/<CR><CR>
vnoremap <silent> # :<C-u>call visual#action('')<CR>?<C-R>=@/<CR><CR>
vnoremap <silent> <leader>f :call visual#action('Ack! foo')<CR>
nnoremap <leader>f :Ack! 
vnoremap <leader>r :call visual#replace('%s/foo//g')<CR><left><left>

if has('mac')
    Plug 'rizzatti/dash.vim', {'on':[
                \  'Dash',
                \  'Dash!'
                \]}
    nnoremap <silent> <leader>z :Dash<cr>
else
    Plug 'KabbAmine/zeavim.vim', {'on': [
                \	'Zeavim', 'Docset',
                \	'<Plug>Zeavim',
                \	'<Plug>ZVVisSelection',
                \	'<Plug>ZVKeyDocset',
                \	'<Plug>ZVMotion'
                \ ]}
    nnoremap <silent> <leader>z :Zeavim<cr>
endif


call plug#end()
